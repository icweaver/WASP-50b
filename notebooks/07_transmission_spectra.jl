### A Pluto.jl notebook ###
# v0.15.1

using Markdown
using InteractiveUtils

# â•”â•â•¡ ef970c0c-d08a-4856-b10b-531bb5e7e53e
begin
	import Pkg
	Pkg.activate(joinpath(@__DIR__, ".."))

	using AlgebraOfGraphics
	using CCDReduction
	using CSV
	using CairoMakie
	using Colors
	using DataFrames
	using DataFramesMeta
	using Dates
	using DelimitedFiles
	using Glob
	using ImageFiltering
	using KernelDensity
	using Latexify
	using Measurements
	using NaturalSort
	using OrderedCollections
	using Printf
	using PyCall
	using Statistics
	using PlutoUI: TableOfContents, Select, Slider, as_svg, with_terminal
end

# â•”â•â•¡ e8b8a0c9-0030-40f2-84e9-7fca3c5ef100
md"""
# Transmission Spectra

In this notebook we will load in the individual transmission spectra from each night, and combine them on a common wavelength basis.

$(TableOfContents(title="ğŸ“– Table of Contents"))
"""

# â•”â•â•¡ 9413e640-22d9-4bfc-b4ea-f41c02a3bfde
md"""
## Load data
"""

# â•”â•â•¡ 5100e6b4-03da-4e58-aad1-13376bcb4b59
md"""
First let's load up all of the data, including the white light transit depths from each night. This will be used for combining the transmission spectra later:
"""

# â•”â•â•¡ c53be9cf-7722-4b43-928a-33e7b0463330
const DATA_DIR = "data/detrended/out_l/WASP50"

# â•”â•â•¡ 1decb49e-a875-412c-938f-74b4fa0e2e85
maxmeasure(x, x_u, x_d) = x Â± max(x_u, x_d)

# â•”â•â•¡ 7b6d3a33-cb3b-4776-86f6-3af1663b9e49
dates_to_names = Dict(
	"131219_IMACS" => "Transit 1 (IMACS)",
	"150927_IMACS" => "Transit 2 (IMACS)",
	"150927_LDSS3_flat" => "Transit 2 (LDSS3)",
	"150927_LDSS3_noflat" => "Transit 2 (LDSS3 noflat)",
	"161211_IMACS" => "Transit 3 (IMACS)",
 )

# â•”â•â•¡ 1e8524c4-a732-4e2f-80a9-b5e7548ef2b2
function name(fpath, data_to_names)
	date_target = splitpath(split(glob(fpath)[1], "w50_")[2])[1]
	return dates_to_names[date_target]
end

# â•”â•â•¡ 5c4fcb25-9a26-43f1-838b-338b33fb9ee6
begin
	cubes = Dict{String, Dict}()
	
	for dirpath in sort(glob("$(DATA_DIR)/w50_*"))
		# Read tspec file
		fpath = "$(dirpath)/transpec.csv"
		transit = name(fpath, dates_to_names)
		
		cubes[transit] = Dict()
		
		df = cubes[transit]["tspec"] = CSV.File(
			fpath,
			normalizenames = true,
		) |> DataFrame
		
		# Add wav bins for external instruments
		if occursin("LDSS3", dirpath)
			wbins = readdlm("$(dirpath)/wbins.dat", comments=true)
			cubes[transit]["tspec"][:, [:Wav_d, :Wav_u]] .= wbins
		end
		
		# For plotting later
		df.wav = mean([df.Wav_u, df.Wav_d])
		df.Î´ = maxmeasure.(df.Depth_ppm_, df.Depthup_ppm_, df.DepthDown_ppm_)
		
		fpath_WLC = "$(dirpath)/white-light/results.dat"
		df_WLC = CSV.File(
			fpath_WLC,
			comment = "#",
			normalizenames = true,
		) |> DataFrame
		
		symbol, p, p_u, p_d = eachcol(
			@where(df_WLC, :Variable .== "p")
		)
	 	
		cubes[transit]["Î´_WLC"] = maxmeasure(p[1], p_u[1], p_d[1])^2 * 1e6
	end
	
	cubes = sort(cubes)
end

# â•”â•â•¡ d6918a50-f75f-47f5-86c6-e251f7ef1e12
cubes.keys

# â•”â•â•¡ e58ec082-d654-44e3-bcd4-906fc34171c8
md"""
## Combine spectra ğŸŒˆ
"""

# â•”â•â•¡ 11066667-9da2-4b36-b784-c3515c04a659
md"""
We start the combining process by saving the subset of the data sharing the same range of wavelength bins:
"""

# â•”â•â•¡ cb1b277b-aa92-44de-91ce-88122bc34bb9
df_common = innerjoin(
	(cube["tspec"] for (transit, cube) in cubes)...,
	on = :wav,
	makeunique = true,
)

# â•”â•â•¡ 461097e9-a687-4ef2-a5b4-8bf4d9e1c98f
antijoins = (
	"Transit 1 (IMACS)" => antijoin(
		cubes["Transit 1 (IMACS)"]["tspec"],
		cubes["Transit 2 (LDSS3)"]["tspec"],
		on = :wav,
	),
	
	"Transit 2 (IMACS)" => antijoin(
		cubes["Transit 2 (IMACS)"]["tspec"],
		cubes["Transit 2 (LDSS3)"]["tspec"],
		on = :wav,
	),
	
	
	"Transit 2 (LDSS3)" => antijoin(
		cubes["Transit 2 (LDSS3)"]["tspec"],
		cubes["Transit 1 (IMACS)"]["tspec"],
		on = :wav,
	),
	
	
	"Transit 3 (IMACS)" => antijoin(
		cubes["Transit 3 (IMACS)"]["tspec"],
		cubes["Transit 2 (LDSS3)"]["tspec"],
		on = :wav,
	),
)

# â•”â•â•¡ 99757e41-0a88-4662-abef-0fad1bbbed1d
md"""
We next take these common transmission spectra and offset each, given by the difference between its corresponding white-light curve transit depth and the average WLC depth across nights:
"""

# â•”â•â•¡ 84055852-1b9f-4221-95a7-ab48110bf78c
depths_common = df_common[!, r"Î´"] |>  x -> rename!(x, cubes.keys);

# â•”â•â•¡ 2f377692-2abf-404e-99ea-a18c7af1a840
wlc_depths = [cube["Î´_WLC"] for (transit, cube) in cubes]

# â•”â•â•¡ c405941d-bdcc-458f-b0bf-01abf02982e0
mean_wlc_depth = mean(wlc_depths)

# â•”â•â•¡ a915f236-8dae-4c91-8f96-fb9a805a0a7f
wlc_offsets = reshape(wlc_depths .- mean_wlc_depth, 1, :)

# â•”â•â•¡ 4b9cfc02-5e18-422d-b18e-6301a659561a
begin
	depths_adj = depths_common .- Measurements.value.(wlc_offsets)
	#depths_adj = copy(depths_common)
	depths_adj.Combined = weightedmean.(eachrow(depths_adj))
	insertcols!(depths_adj, 1,
		:Wav_d => df_common.Wav_d,
		:Wav_u => df_common.Wav_u,
		:Wav_cen => df_common.wav,
	)
end;

# â•”â•â•¡ 5d25caa3-916a-40b1-ba7c-ea1295afb775
md"""
Average precision per bin: $(round(Int, getproperty.(depths_adj[!, :Combined], :err) |> median)) ppm
"""

# â•”â•â•¡ f8a86915-f7d8-4462-980e-7b8124b13a3f
md"""
## Plot configs
"""

# â•”â•â•¡ bef0918c-c645-4557-a2e5-00b6c26573bc
begin
	const FIG_TALL = (900, 1_200)
	const FIG_WIDE = (1_350, 800)
	#const COLORS = to_colormap(:seaborn_colorblind6, 8)[[8, 6, 4, 1]]
	const COLORS = parse.(Colorant,
		[
			"#a6cee3",  # Cyan
			"#fdbf6f",  # Yellow
			"#ff7f00",  # Orange
			"#1f78b4",  # Blue
			# "plum",
			# "#956cb4",  # Purple
			# "mediumaquamarine",
			# "#029e73",  # Green
		]
	)
	
	set_aog_theme!()
	update_theme!(
		Theme(
			Axis = (xlabelsize=18, ylabelsize=18,),
			Label = (textsize=18,),
			Lines = (linewidth=3, cycle=Cycle([:color, :linestyle], covary=true)),
			Scatter = (linewidth=10,),
			palette = (color=COLORS, patchcolor=[(c, 0.35) for c in COLORS]),
			fontsize = 18,
			rowgap = 0,
			colgap = 0,
		)
	)
	
	COLORS
end

# â•”â•â•¡ 8c077881-fc5f-4fad-8497-1cb6106c6ed5
let
	fig = Figure(resolution=(1_000, 500))
		
	ax = Axis(
		fig[1, 1], xlabel="Wavelength (Ã…)", ylabel="Transit depth (ppm)",
		limits = (nothing, (15_500, 22_500)),
		grid = (linewidth=(0, 0),),
	)
	
	vlines!(ax, [5892.9, 7682.0, 8189.0], color=:grey, linestyle=:dash)
	hlines!(ax, mean_wlc_depth.val, color=:grey, linestyle=:dash, linewidth=3)
	hlines!(ax, mean_wlc_depth.val + mean_wlc_depth.err, color=:grey, linewidth=3)
	hlines!(ax, mean_wlc_depth.val - mean_wlc_depth.err, color=:grey, linewidth=3)
	
	wav = depths_adj.Wav_cen
	Î”WLC_depth = mean_wlc_depth.err
	
	# Individual nights
	for (i, transit) in enumerate(keys(cubes))
		tspec = depths_adj[!, transit]
		depth = Measurements.value.(tspec)
		depth_err = Measurements.uncertainty.(tspec)
		
		errorbars!(ax, wav, depth, depth_err;
			color = COLORS[i],
			linewidth = 3,
		)
		scatter!(ax, wav, depth;
			color = COLORS[i],
			strokewidth = 0,
			markersize = 12,
			label = transit,
		)
	end
	
	# Combined
	tspec_combined = depths_adj.Combined
	depth_combined = Measurements.value.(tspec_combined)
	depth_combined_err = Measurements.uncertainty.(tspec_combined)

	errorbars!(ax, wav, depth_combined, depth_combined_err;
		linewidth = 5,
		whiskerwidth = 10,
	)
	
	scatter!(ax, wav, depth_combined;
		color = :white,
		strokewidth = 3,
		markersize = 12,
		label = "Combined",
	)
		
	# Plot uncombined points
	for (i, (transit, df)) in enumerate(antijoins)
		wav = df.wav
		f = Measurements.value.(df.Î´)
		f_err = Measurements.uncertainty.(df.Î´)
		errorbars!(ax, wav, f, f_err;
			linewidth = 3,
			color = (COLORS[i], 0.5),
		)

		scatter!(ax, wav, f;
			color = (COLORS[i], 0.5),
			markersize = 12,
		)
	end

	axislegend(orientation=:horizontal, valign=:bottom, labelsize=16)
		
	fig #|> as_svg
end

# â•”â•â•¡ 3510ead9-6e66-4fec-84ca-15c8a3ce4c3e
html"""
<style>
#launch_binder {
	display: none;
}
body.disable_ui main {
		max-width : 95%;
	}
@media screen and (min-width: 1081px) {
	body.disable_ui main {
		margin-left : 10px;
		max-width : 72%;
		align-self: flex-start;
	}
}
</style>
"""

# â•”â•â•¡ Cell order:
# â•Ÿâ”€e8b8a0c9-0030-40f2-84e9-7fca3c5ef100
# â•Ÿâ”€9413e640-22d9-4bfc-b4ea-f41c02a3bfde
# â•Ÿâ”€5100e6b4-03da-4e58-aad1-13376bcb4b59
# â• â•c53be9cf-7722-4b43-928a-33e7b0463330
# â• â•d6918a50-f75f-47f5-86c6-e251f7ef1e12
# â• â•5c4fcb25-9a26-43f1-838b-338b33fb9ee6
# â• â•1decb49e-a875-412c-938f-74b4fa0e2e85
# â• â•1e8524c4-a732-4e2f-80a9-b5e7548ef2b2
# â• â•7b6d3a33-cb3b-4776-86f6-3af1663b9e49
# â•Ÿâ”€e58ec082-d654-44e3-bcd4-906fc34171c8
# â•Ÿâ”€11066667-9da2-4b36-b784-c3515c04a659
# â• â•cb1b277b-aa92-44de-91ce-88122bc34bb9
# â• â•461097e9-a687-4ef2-a5b4-8bf4d9e1c98f
# â•Ÿâ”€99757e41-0a88-4662-abef-0fad1bbbed1d
# â• â•84055852-1b9f-4221-95a7-ab48110bf78c
# â• â•2f377692-2abf-404e-99ea-a18c7af1a840
# â• â•c405941d-bdcc-458f-b0bf-01abf02982e0
# â• â•a915f236-8dae-4c91-8f96-fb9a805a0a7f
# â• â•4b9cfc02-5e18-422d-b18e-6301a659561a
# â•Ÿâ”€5d25caa3-916a-40b1-ba7c-ea1295afb775
# â• â•8c077881-fc5f-4fad-8497-1cb6106c6ed5
# â•Ÿâ”€f8a86915-f7d8-4462-980e-7b8124b13a3f
# â• â•bef0918c-c645-4557-a2e5-00b6c26573bc
# â• â•ef970c0c-d08a-4856-b10b-531bb5e7e53e
# â•Ÿâ”€3510ead9-6e66-4fec-84ca-15c8a3ce4c3e
